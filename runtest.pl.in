#!@PERL@

use strict;
use warnings;

use lib 'infrastructure';
use BoxPlatform;

my ($test_name,$test_mode) = @ARGV;

$test_mode = 'debug' if not defined $test_mode or $test_mode eq '';

if($test_name eq '' || ($test_mode ne 'debug' && $test_mode ne 'release'))
{
	print <<__E;
Run Test utility -- bad usage.

runtest.pl (test|ALL) [release|debug]

Mode defaults to debug.

__E
	exit(2);
}

my @results;
my $exit_code = 0;

if($test_name ne 'ALL')
{
	# run one or more specified test
	if ($test_name =~ m/,/)
	{
		foreach my $test (split m/,/, $test_name)
		{
			runtest($test);
		}
	}
	else
	{
		runtest($test_name);
	}
}
else
{
	# run all tests
	my @tests;
	open MODULES,'modules.txt' or die "Can't open modules file";
	while(<MODULES>)
	{
		# omit bits on some platforms?
		next if m/\AEND-OMIT/;
		if(m/\AOMIT:(.+)/)
		{
			if($1 eq $build_os or $1 eq $target_os)
			{
				while(<MODULES>)
				{
					last if m/\AEND-OMIT/;	
				}
			}
			next;
		}
		push @tests,$1 if m~\Atest/(\w+)\s~;
	}
	close MODULES;
	
	runtest($_) for(@tests)
}

# report results
print "--------\n",join("\n",@results),"\n";

if ($exit_code != 0)
{
	print <<__E;

One or more tests have failed. Please check the following common causes:

* Check that no instances of bbstored or bbackupd are already running
  on this machine.
* Make sure there isn't a firewall blocking incoming or outgoing connections
  on port 2201.
* Check that there is sufficient space in the filesystem that the tests
  are being run from (at least 1 GB free).
* The backupdiff test fails if it takes too long, so it's sensitive to
  the speed of the host and your connection to it.

After checking all the above, if you still have problems please contact
us on the mailing list, boxbackup\@fluffy.co.uk. Thanks!
__E
}

exit $exit_code;

sub runtest
{
	my ($t) = @_;

	# attempt to make this test
	my $flag = ($test_mode eq 'release')?(BoxPlatform::make_flag('RELEASE')):'';
	my $make_res = system("cd test/$t ; $make_command $flag");
	if($make_res != 0)
	{
		push @results,"$t: make failed";
		$exit_code = 2;
		return;
	}

	my $logfile = "test-$t.log";
	
	# run it
	my $test_res = system("cd $test_mode/test/$t ; ./t 2>&1 " .
		"| tee ../../../$logfile");

	# open test results
	if(open RESULTS, $logfile)
	{
		my $last;
		while(<RESULTS>)
		{
			$last = $_ if m/\w/;
		}
		close RESULTS;

		chomp $last;
		push @results, "$t: $last";

		if ($last ne "PASSED") 
		{ 
			$exit_code = 1;
		}
	}
	else
	{
		push @results, 
			"$t: failed to open test log file: $logfile: $!";
	}
	
	# delete test results
	# unlink $logfile;
}

